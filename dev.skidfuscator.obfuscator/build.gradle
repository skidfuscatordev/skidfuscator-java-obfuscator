plugins {
    id 'java'
    id 'com.roscopeco.jasm' version "0.7.0"
    id 'xyz.wagyourtail.jvmdowngrader' version '1.2.1'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    testCompile.extendsFrom compileOnly
}

dependencies {
    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    compileOnly 'org.projectlombok:lombok:1.18.36'

    api project(':commons')
    api project(':pure-analysis')

    implementation project(':sdk')
    implementation 'com.github.lukfor:magic-progress:0.3.2'
    implementation 'com.github.matomo-org:matomo-java-tracker:v1.7'
    api 'com.github.Col-E:jphantom:1.4.3'
    implementation 'dev.dirs:directories:26'
    implementation 'de.femtopedia.dex2jar:dex2jar:2.4.24'
    implementation 'org.apache.commons:commons-compress:1.24.0'

    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
    implementation 'org.fusesource.jansi:jansi:2.4.0'
    implementation 'org.yaml:snakeyaml:1.33'

    testImplementation files("$buildDir/classes/jasm/test")
    testImplementation 'org.mockito:mockito-core:4.2.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}


jar {
    into('resources') {
        from project(':sdk').tasks.jar.archiveFile
        rename { String filename ->
            'sdk.jar'
        }
    }
}

// Add SDK jar to test resources
processTestResources {
    from(project(':sdk').tasks.jar.archiveFile) {
        into 'resources'
        rename { String filename ->
            'sdk.jar'
        }
    }
}

test {
    useJUnitPlatform()
}

configurations {
    downgrade
    implementation.extendsFrom downgrade
}

downgradeJar {
    downgradeTo = JavaVersion.VERSION_1_8
}